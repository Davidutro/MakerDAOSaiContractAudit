# mom-b353893

Source file [../../contracts/makerdao/mom-b353893.sol](../../contracts/makerdao/mom-b353893.sol).

Repository https://github.com/makerdao/sai/blob/b353893f5ea8ee9811a446222c49b4812365cfe9/src/mom.sol

<br />

<hr />

```javascript
/// mom.sol -- admin manager

// Copyright (C) 2017  Nikolai Mushegian <nikolai@dapphub.com>
// Copyright (C) 2017  Daniel Brockman <daniel@dapphub.com>
// Copyright (C) 2017  Rain <rainbreak@riseup.net>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

// BK Ok
// BK NOTE - The premissions in the // BK NOTE below are scraped from the blockchain using scripts in the ./audit/scripts folder
pragma solidity ^0.4.18;

// BK Next 4 Ok
import 'ds-thing/thing.sol';
import './tub.sol';
import './top.sol';
import './tap.sol';


// BK Ok
// BK NOTE - mom.owner=0x0000000000000000000000000000000000000000
// BK NOTE - mom.authority=adm:0x8e2a84d6ade1e7fffee039a35ef5f19f13057152
// BK NOTE - ChiefAdm allows voting for a `hat`, and the `hat` can execute a `spell` transaction to trigger a call to one of the functions below
contract SaiMom is DSThing {
    // BK Next 3 Ok
    SaiTub  public  tub;
    SaiTap  public  tap;
    SaiVox  public  vox;

    // BK Ok - Constructor
    function SaiMom(SaiTub tub_, SaiTap tap_, SaiVox vox_) public {
        // BK Next 3 Ok
        tub = tub_;
        tap = tap_;
        vox = vox_;
    }
    // Debt ceiling
    // BK Ok - Only adm can execute
    // BK NOTE - Eg https://etherscan.io/tx/0x95b36176e2aad6545c829e5d9ef85a0d0392bd4f0b373d2aae4607d40423cdc0 setCap(50,000,000)
    // BK NOTE - Executed by dsspell https://etherscan.io/address/0x347e94e12c623d7b9d51b3f143ff42b73d619773
    // BK NOTE - Voted by dsChiefAdm and spell executed
    function setCap(uint wad) public note auth {
        // BK Ok
        // BK NOTE - Permit from mom:0xf2c5369cffb8ea6284452b0326e326dbfdcb867c to tub:0x448a5065aebb8e423f0896e6c5d525c040f59af3 for mold(bytes32,uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5
        // BK NOTE - tub.cap=50000000000000000000000000 50000000 sai
        tub.mold("cap", wad);
    }
    // Liquidation ratio
    // BK Ok - Only adm can execute
    function setMat(uint ray) public note auth {
        // BK Ok - 150% collateral
        // BK NOTE - tub.mat=1500000000000000000000000000 1.5 ray
        tub.mold("mat", ray);
        // BK Ok
        // BK NOTE - tub.axe=1130000000000000000000000000 1.13 ray
        var axe = tub.axe();
        // BK Ok
        var mat = tub.mat();
        // BK Ok
        require(axe >= RAY && axe <= mat);
    }
    // Stability fee
    // BK Ok - Only adm can execute
    function setTax(uint ray) public note auth {
        // BK Ok
        // BK NOTE - tub.tax=1000000000000000000000000000 1 ray
        tub.mold("tax", ray);
        // BK Ok
        var tax = tub.tax();
        // BK Ok
        require(RAY <= tax);
        // BK Ok
        require(tax < 1000001100000000000000000000);  // 10% / day
    }
    // Governance fee
    // BK Ok - Only adm can execute
    function setFee(uint ray) public note auth {
        // BK Ok
        // BK NOTE - tub.fee=1000000000158153903837946257 1.000000000158153903837946257 ray
        tub.mold("fee", ray);
        // BK Ok
        var fee = tub.fee();
        // BK Ok
        require(RAY <= fee);
        // BK Ok
        require(fee < 1000001100000000000000000000);  // 10% / day
    }
    // Liquidation fee
    // BK Ok - Only adm can execute
    function setAxe(uint ray) public note auth {
        // BK Ok
        // BK NOTE - tub.axe=1130000000000000000000000000 1.13 ray
        tub.mold("axe", ray);
        // BK Next 2 Ok
        var axe = tub.axe();
        var mat = tub.mat();
        // BK Ok
        require(axe >= RAY && axe <= mat);
    }
    // Join/Exit Spread
    // BK Ok - Only adm can execute
    function setTubGap(uint wad) public note auth {
        // BK Ok
        // BK NOTE - tub.gap=1000000000000000000 1.000000000000000000
        tub.mold("gap", wad);
    }
    // ETH/USD Feed
    // BK Ok - Only adm can execute
    function setPip(DSValue pip_) public note auth {
        // BK Ok
        // BK NOTE - Permit from mom:0xf2c5369cffb8ea6284452b0326e326dbfdcb867c to tub:0x448a5065aebb8e423f0896e6c5d525c040f59af3 for setPip(address) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb
        // BK NOTE - tub.pip=pip:0x729d19f657bd0614b4985cf1d82531c67569197b
        tub.setPip(pip_);
    }
    // MKR/USD Feed
    // BK Ok - Only adm can execute
    function setPep(DSValue pep_) public note auth {
        // BK Ok
        // BK NOTE - Permit from mom:0xf2c5369cffb8ea6284452b0326e326dbfdcb867c to tub:0x448a5065aebb8e423f0896e6c5d525c040f59af3 for setPep(address) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb
        // BK NOTE - tub.pep=pep:0x99041f808d598b782d5a3e498681c2452a31da08
        tub.setPep(pep_);
    }
    // TRFM
    // BK Ok - Only adm can execute
    function setVox(SaiVox vox_) public note auth {
        // BK Ok
        // BK NOTE - Permit from mom:0xf2c5369cffb8ea6284452b0326e326dbfdcb867c to tub:0x448a5065aebb8e423f0896e6c5d525c040f59af3 for setVox(address) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb
        // BK NOTE - tub.vox=vox:0x9b0f70df76165442ca6092939132bbaea77f2d7a
        tub.setVox(vox_);
    }
    // Boom/Bust Spread
    // BK Ok - Only adm can execute
    function setTapGap(uint wad) public note auth {
        // BK Ok
        // BK NOTE - Permit from mom:0xf2c5369cffb8ea6284452b0326e326dbfdcb867c to tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef for mold(bytes32,uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5
        tap.mold("gap", wad);
        // BK Ok
        // BK NOTE - tap.gap=970000000000000000 0.97
        var gap = tap.gap();
        // BK Next 2 Ok
        require(gap <= 1.05 ether);
        require(gap >= 0.95 ether);
    }
    // Rate of change of target price (per second)
    // BK Ok - Only adm can execute
    function setWay(uint ray) public note auth {
        // BK Ok - 1.0000011 ^ (60 * 60 * 24) = 1.099702784876423 = ~ 1 + 10%
        require(ray < 1000001100000000000000000000);  // 10% / day
        // BK Ok - 0.9999988 ^ (60 * 60 * 24) = 0.901513679582627 = ~ 1 - 10% 
        require(ray >  999998800000000000000000000);
        // BK Ok
        // BK NOTE - Permit from mom:0xf2c5369cffb8ea6284452b0326e326dbfdcb867c to vox:0x9b0f70df76165442ca6092939132bbaea77f2d7a for mold(bytes32,uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5
        vox.mold("way", ray);
    }
    // BK Ok - Only adm can execute
    function setHow(uint ray) public note auth {
        // BK Ok
        // BK NOTE - Permit from mom:0xf2c5369cffb8ea6284452b0326e326dbfdcb867c to vox:0x9b0f70df76165442ca6092939132bbaea77f2d7a for tune(uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
        // BK NOTE - vox.how=0
        vox.tune(ray);
    }
}

```
