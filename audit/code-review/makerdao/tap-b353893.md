# tap-b353893

Source file [../../contracts/makerdao/tap-b353893.sol](../../contracts/makerdao/tap-b353893.sol).

Repository https://github.com/makerdao/sai/blob/b353893f5ea8ee9811a446222c49b4812365cfe9/src/tap.sol

<br />

<hr />

```javascript
/// tap.sol -- liquidation engine (see also `vow`)

// Copyright (C) 2017  Nikolai Mushegian <nikolai@dapphub.com>
// Copyright (C) 2017  Daniel Brockman <daniel@dapphub.com>
// Copyright (C) 2017  Rain Break <rainbreak@riseup.net>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

// BK Ok
pragma solidity ^0.4.18;

// BK Ok
import "./tub.sol";

// BK Ok
// BK NOTE - tap.owner=0x0000000000000000000000000000000000000000
// BK NOTE - tap.authority=dad:0x315cbb88168396d12e1a255f9cb935408fe80710
contract SaiTap is DSThing {
    // BK Next 3 Ok
    // BK NOTE - tap.sai=sai:0x89d24a6b4ccb1b6faa2625fe562bdd9a23260359
    // BK NOTE - tap.sin=sin:0x79f6d0f646706e1261acf0b93dcb864f357d4680
    // BK NOTE - tap.skr=skr:0xf53ad2c6851052a81b42133467480961b2321c09
    DSToken  public  sai;
    DSToken  public  sin;
    DSToken  public  skr;

    // BK Next 2 Ok
    // BK NOTE - tap.vox=vox:0x9b0f70df76165442ca6092939132bbaea77f2d7a
    // BK NOTE - tap.tub=tub:0x448a5065aebb8e423f0896e6c5d525c040f59af3
    SaiVox   public  vox;
    SaiTub   public  tub;

    // BK Next 3 Ok
    // BK NOTE - tap.gap=970000000000000000 0.97
    // BK NOTE - tap.off=false
    // BK NOTE - tap.fix=0
    uint256  public  gap;  // Boom-Bust Spread
    bool     public  off;  // Cage flag
    uint256  public  fix;  // Cage price

    // Surplus
    // BK Ok
    // BK NOTE - tap.joy=133
    function joy() public view returns (uint) {
        // BK Ok
        return sai.balanceOf(this);
    }
    // Bad debt
    // BK Ok
    // BK NOTE - tap.woe=0
    function woe() public view returns (uint) {
        // BK Ok
        return sin.balanceOf(this);
    }
    // Collateral pending liquidation
    // BK NOTE - tap.fog=0
    function fog() public view returns (uint) {
        // BK Ok
        return skr.balanceOf(this);
    }


    // BK Ok - Constructor
    function SaiTap(SaiTub tub_) public {
        // BK Ok
        tub = tub_;

        // BK Next 3 Ok
        sai = tub.sai();
        sin = tub.sin();
        skr = tub.skr();

        // BK Ok
        vox = tub.vox();

        // BK Ok
        gap = WAD;
    }

    // BK Ok - adm can execute `mom.setTapGap(...)` that will change ths boom-bust spread parameter
    // BK NOTE - Permit from mom:0xf2c5369cffb8ea6284452b0326e326dbfdcb867c to tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef for mold(bytes32,uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
    function mold(bytes32 param, uint val) public note auth {
        // BK Ok
        if (param == 'gap') gap = val;
    }

    // Cancel debt
    // BK NOTE - Permit from tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef to sai:0x89d24a6b4ccb1b6faa2625fe562bdd9a23260359 for burn(uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
    // BK NOTE - Permit from tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef to sin:0x79f6d0f646706e1261acf0b93dcb864f357d4680 for burn(uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
    function heal() public note {
        if (joy() == 0 || woe() == 0) return;  // optimised
        var wad = min(joy(), woe());
        sai.burn(wad);
        sin.burn(wad);
    }

    // Feed price (sai per skr)
    function s2s() public returns (uint) {
        var tag = tub.tag();    // ref per skr
        var par = vox.par();    // ref per sai
        return rdiv(tag, par);  // sai per skr
    }
    // Boom price (sai per skr)
    function bid(uint wad) public returns (uint) {
        return rmul(wad, wmul(s2s(), sub(2 * WAD, gap)));
    }
    // Bust price (sai per skr)
    function ask(uint wad) public returns (uint) {
        return rmul(wad, wmul(s2s(), gap));
    }
    function flip(uint wad) internal {
        require(ask(wad) > 0);
        skr.push(msg.sender, wad);
        sai.pull(msg.sender, ask(wad));
        heal();
    }
    // BK NOTE - Permit from tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef to skr:0xf53ad2c6851052a81b42133467480961b2321c09 for mint(uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
    function flop(uint wad) internal {
        skr.mint(sub(wad, fog()));
        flip(wad);
        require(joy() == 0);  // can't flop into surplus
    }
    // BK NOTE - Permit from tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef to skr:0xf53ad2c6851052a81b42133467480961b2321c09 for burn(address,uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
    function flap(uint wad) internal {
        heal();
        sai.push(msg.sender, bid(wad));
        skr.burn(msg.sender, wad);
    }
    function bust(uint wad) public note {
        require(!off);
        if (wad > fog()) flop(wad);
        else flip(wad);
    }
    function boom(uint wad) public note {
        require(!off);
        flap(wad);
    }

    //------------------------------------------------------------------

    // BK NOTE - Permit from top:0x9b0ccf7c8994e19f39b2b4cf708e0a7df65fa8a3 to tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef for cage(uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
    function cage(uint fix_) public note auth {
        require(!off);
        off = true;
        fix = fix_;
    }
    // BK NOTE - Permit from tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef to sai:0x89d24a6b4ccb1b6faa2625fe562bdd9a23260359 for burn(address,uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
    function cash(uint wad) public note {
        require(off);
        sai.burn(msg.sender, wad);
        require(tub.gem().transfer(msg.sender, rmul(wad, fix)));
    }
    // BK NOTE - Permit from tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef to sai:0x89d24a6b4ccb1b6faa2625fe562bdd9a23260359 for mint(address,uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
    function mock(uint wad) public note {
        require(off);
        sai.mint(msg.sender, wad);
        require(tub.gem().transferFrom(msg.sender, this, rmul(wad, fix)));
    }
    // BK NOTE - Permit from tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef to skr:0xf53ad2c6851052a81b42133467480961b2321c09 for burn(uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
    function vent() public note {
        require(off);
        skr.burn(fog());
    }
}

```
