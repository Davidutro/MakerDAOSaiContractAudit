# tap-b353893

Source file [../../contracts/makerdao/tap-b353893.sol](../../contracts/makerdao/tap-b353893.sol).

Repository https://github.com/makerdao/sai/blob/b353893f5ea8ee9811a446222c49b4812365cfe9/src/tap.sol

<br />

<hr />

```javascript
/// tap.sol -- liquidation engine (see also `vow`)

// Copyright (C) 2017  Nikolai Mushegian <nikolai@dapphub.com>
// Copyright (C) 2017  Daniel Brockman <daniel@dapphub.com>
// Copyright (C) 2017  Rain Break <rainbreak@riseup.net>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

// BK Ok
pragma solidity ^0.4.18;

// BK Ok
import "./tub.sol";

// BK NOTE - `tap`: The Liquidator
// BK NOTE - tap.owner=0x0000000000000000000000000000000000000000
// BK NOTE - tap.authority=dad:0x315cbb88168396d12e1a255f9cb935408fe80710
// BK Ok
contract SaiTap is DSThing {
    // BK Next 3 Ok
    // BK NOTE - tap.sai=sai:0x89d24a6b4ccb1b6faa2625fe562bdd9a23260359
    // BK NOTE - tap.sin=sin:0x79f6d0f646706e1261acf0b93dcb864f357d4680
    // BK NOTE - tap.skr=skr:0xf53ad2c6851052a81b42133467480961b2321c09
    DSToken  public  sai;
    DSToken  public  sin;
    DSToken  public  skr;

    // BK Next 2 Ok
    // BK NOTE - tap.vox=vox:0x9b0f70df76165442ca6092939132bbaea77f2d7a
    // BK NOTE - tap.tub=tub:0x448a5065aebb8e423f0896e6c5d525c040f59af3
    SaiVox   public  vox;
    SaiTub   public  tub;

    // BK Next 3 Ok
    // BK NOTE - tap.gap=970000000000000000 0.97 wad
    // BK NOTE - tap.off=false
    // BK NOTE - tap.fix=0
    uint256  public  gap;  // Boom-Bust Spread
    bool     public  off;  // Cage flag
    uint256  public  fix;  // Cage price

    // Surplus
    // BK Ok - View function
    // BK NOTE - `joy`: Sai balance, surplus transferred from `drip`
    // BK NOTE - tap.joy=521 5.21e-16 wad
    function joy() public view returns (uint) {
        // BK Ok
        return sai.balanceOf(this);
    }
    // Bad debt
    // BK Ok - View function
    // BK NOTE - `woe`: Sin balance, bad debt transferred from `bite`
    // BK NOTE - tap.woe=0 0 wad
    function woe() public view returns (uint) {
        // BK Ok
        return sin.balanceOf(this);
    }
    // Collateral pending liquidation
    // BK Ok - `fog`: SKR balance, collateral pending liquidation
    // BK NOTE - tap.fog=1000000000001 0.000001000000000001 wad
    function fog() public view returns (uint) {
        // BK Ok
        return skr.balanceOf(this);
    }


    // BK Ok - Constructor
    function SaiTap(SaiTub tub_) public {
        // BK Ok
        tub = tub_;

        // BK Next 3 Ok
        sai = tub.sai();
        sin = tub.sin();
        skr = tub.skr();

        // BK Ok
        vox = tub.vox();

        // BK Ok
        gap = WAD;
    }

    // BK Ok - adm can execute `mom.setTapGap(...)` that will change ths boom-bust spread parameter
    // BK NOTE - Permit from mom:0xf2c5369cffb8ea6284452b0326e326dbfdcb867c to tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef for mold(bytes32,uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
    function mold(bytes32 param, uint val) public note auth {
        // BK Ok
        if (param == 'gap') gap = val;
    }

    // Cancel debt
    // BK NOTE - Sai can be canceled out with Sin via `heal`
    // BK Ok - Any account can execute to cancel the minimum of Sai and Sin balances
    function heal() public note {
        // BK - Nothing to cancel
        // BK NOTE - joy() = sai.balanceOf(this)
        // BK NOTE - woe() = sin.balanceOf(this)
        if (joy() == 0 || woe() == 0) return;  // optimised
        // BK Ok
        var wad = min(joy(), woe());
        // BK Ok
        // BK NOTE - Permit from tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef to sai:0x89d24a6b4ccb1b6faa2625fe562bdd9a23260359 for burn(uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
        sai.burn(wad);
        // BK Ok
        // BK NOTE - Permit from tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef to sin:0x79f6d0f646706e1261acf0b93dcb864f357d4680 for burn(uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
        sin.burn(wad);
    }

    // Feed price (sai per skr)
    // BK NOTE - `s2s`, which is the price of SKR in Sai
    // BK NOTE - s2s=609407526508371291433306584991 609407526508.371291433306584991 609.407526508371291433306584991
    // BK NOTE - `s2s` is ray
    // BK Ok
    function s2s() public returns (uint) {
        // BK NOTE - tub.tag=609407526508371291433306584991 609407526508.371291433306584991 609.407526508371291433306584991 
        // BK Ok - `tag` is ray
        var tag = tub.tag();    // ref per skr
        // BK NOTE - par=1000000000000000000000000000 1000000000 1
        // BK Ok - `par` is ray
        var par = vox.par();    // ref per sai
        // BK NOTE - result is 609407526508371291433306584991
        // BK Ok
        return rdiv(tag, par);  // sai per skr
    }
    // Boom price (sai per skr)
    // BK NOTE - wad=1000000000000000000000000 1000000 0.001
    // BK NOTE - bid=627689752303622430176305783 627689752.303622430176305783 0.627689752303622430176305783
    // BK NOTE - `bid` is wad
    // BK Ok
    function bid(uint wad) public returns (uint) {
        // BK NOTE - tap.gap=970000000000000000 0.97 wad
        // BK NOTE - sub(2 * WAD, gap) = 1030000000000000000
        // BK NOTE - s2s=609407526508371291433306584991 609407526508.371291433306584991 609.407526508371291433306584991
        // BK NOTE - wmul(s2s(), sub(2 * WAD, gap)) = 627689752303622430176305782541
        // BK NOTE - Result =       627689752303622430176 for 1 wad
        // BK NOTE -       or 627689752303622430176305783 for 1,000,000 wad
        // BK Ok
        return rmul(wad, wmul(s2s(), sub(2 * WAD, gap)));
    }
    // Bust price (sai per skr)
    // BK NOTE - wad=1000000000000000000000000 1000000 0.001
    // BK NOTE - ask=591125300713120152690307387 591125300.713120152690307387 0.591125300713120152690307387
    // BK NOTE - `ask` is wad
    function ask(uint wad) public returns (uint) {
        // BK NOTE - tap.gap=970000000000000000 0.97 wad
        // BK NOTE - s2s=609407526508371291433306584991 609407526508.371291433306584991 609.407526508371291433306584991
        // BK NOTE - wmul(s2s(), gap) = 591125300713120152690307387441
        // BK NOTE - Result =       591125300713120152690 for 1 wad
        // BK NOTE -       or 591125300713120152690307387 for 1,000,000 wad
        // BK Ok
        return rmul(wad, wmul(s2s(), gap));
    }
    // BK NOTE - `bust` is really two functions in one: collateral sell off (aka `flip`), and inflate and sell (aka `flop`)
    // BK Ok - Internal function, only called by `bust(...)` and `flop(...)`
    function flip(uint wad) internal {
        // BK Ok
        require(ask(wad) > 0);
        // BK NOTE - `push(address dst, uint wad)` is `transferFrom(msg.sender, dst, wad)`
        // BK NOTE - `push(msg.sender, wad)` is `transferFrom(address(tap), msg.sender, wad)`
        // BK Ok
        skr.push(msg.sender, wad);
        // BK NOTE - `pull(address src, uint wad)` is `transferFrom(src, msg.sender, wad)`
        // BK NOTE - `pull(msg.sender, wad)` is `transferFrom(msg.sender, address(tap), wad)`
        // BK Ok
        sai.pull(msg.sender, ask(wad));
        // BK Ok - Cancel Sai and Sin
        heal();
    }
    // BK NOTE - `bust` is really two functions in one: collateral sell off (aka `flip`), and inflate and sell (aka `flop`)
    // BK Ok - Internal function, only called by `bust(...)`
    function flop(uint wad) internal {
        // BK NOTE - Permit from tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef to skr:0xf53ad2c6851052a81b42133467480961b2321c09 for mint(uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
        // BK NOTE - fog() = skr.balanceOf(this)
        // BK Ok
        skr.mint(sub(wad, fog()));
        // BK Ok
        flip(wad);
        // BK NOTE - joy() = sai.balanceOf(this)
        // BK Ok
        require(joy() == 0);  // can't flop into surplus
    }
    // BK Ok - Internal function, only called by `boom(...)`
    function flap(uint wad) internal {
        // BK Ok - Cancel Sai and Sin
        heal();
        // BK NOTE - `push(address dst, uint wad)` is `transferFrom(msg.sender, dst, wad)`
        // BK NOTE - `push(msg.sender, wad)` is `transferFrom(address(tap), msg.sender, wad)`
        // BK Ok
        sai.push(msg.sender, bid(wad));
        // BK NOTE - Permit from tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef to skr:0xf53ad2c6851052a81b42133467480961b2321c09 for burn(address,uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
        // BK Ok
        skr.burn(msg.sender, wad);
    }
    // BK NOTE - `bust`: sell SKR in return for Sai (decreases `fog`, increases `joy` and `woe`, can increase SKR supply)
    // BK NOTE - `bust` is really two functions in one: collateral sell off (aka `flip`),
    // BK NOTE - and inflate and sell (aka `flop`). When `fog` is non zero it is sold in
    // BK NOTE - return for Sai, which is used to cancel out the bad debt, `woe`. If
    // BK NOTE - `fog` is zero but the `tap` has a net Sin balance, then SKR is minted
    // BK NOTE - and sold in return for Sai, up to the point that the net Sin balance is
    // BK NOTE - zero.
    // BK Ok - Any account can execute
    function bust(uint wad) public note {
        // BK Ok - Can only be done when operating normally
        require(!off);
        // BK NOTE - fog() = skr.balanceOf(this)
        // BK Ok
        if (wad > fog()) flop(wad);
        // BK Ok
        else flip(wad);
    }
    // BK NOTE - `boom`: sell Sai in return for SKR (decreases `joy` and `woe`, decreases SKR supply)
    // BK NOTE - `boom` is the simpler function and can be thought of as buy and burn.
    // BK NOTE - Given a net Sai balance, sell the Sai in return for SKR, which is
    // BK NOTE - burned.
    function boom(uint wad) public note {
        // BK Ok - Can only be done when operating normally
        require(!off);
        // BK Ok
        flap(wad);
    }

    //------------------------------------------------------------------

    // BK Ok - Global Settlement. Can only be called by `top.cage(...)`
    // BK NOTE - `cage`: shutting down the system and reimbursing Sai holders
    // BK NOTE - Permit from top:0x9b0ccf7c8994e19f39b2b4cf708e0a7df65fa8a3 to tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef for cage(uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
    // BK NOTE - fix_ ~ 1660839886730719724964915
    function cage(uint fix_) public note auth {
        // BK Ok - Can only call `cage(...)` once
        require(!off);
        // BK Ok - Can only call `cage(...)` once
        off = true;
        // BK Ok - `fix`: gem per sai after `cage`
        fix = fix_;
    }
    // BK NOTE - When `cage`, sufficient real `gem` collateral is taken such that Sai holders can redeem their Sai at 
    // BK NOTE - face value. The `gem` is moved from the `tub` to the `tap` and the `tap.cash` function is unlocked
    // BK NOTE - for Sai holders to call.
    // BK Ok - Any account can call to burn Sai and get back a proportion of Gem
    function cash(uint wad) public note {
        // BK Ok - Can only be done after Global Settlement
        require(off);
        // BK Ok
        // BK NOTE - Permit from tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef to sai:0x89d24a6b4ccb1b6faa2625fe562bdd9a23260359 for burn(address,uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
        sai.burn(msg.sender, wad);
        // BK Ok
        require(tub.gem().transfer(msg.sender, rmul(wad, fix)));
    }
    // BK Ok - Any account can execute to mint Sai from Gem (must already be `approve(...)`d)
    function mock(uint wad) public note {
        // BK Ok - Can only be done after Global Settlement
        require(off);
        // BK Ok - Mint Sai
        // BK NOTE - Permit from tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef to sai:0x89d24a6b4ccb1b6faa2625fe562bdd9a23260359 for mint(address,uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
        sai.mint(msg.sender, wad);
        // BK Ok
        require(tub.gem().transferFrom(msg.sender, this, rmul(wad, fix)));
    }
    // BK Ok - Any account can execute
    function vent() public note {
        // BK Ok - Can only be done after Global Settlement
        require(off);
        // BK Ok - `fog`: SKR balance, collateral pending liquidation
        // BK NOTE - Permit from tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef to skr:0xf53ad2c6851052a81b42133467480961b2321c09 for burn(uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
        skr.burn(fog());
    }
}

```
