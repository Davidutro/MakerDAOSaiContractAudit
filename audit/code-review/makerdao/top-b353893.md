# top-b353893

Source file [../../contracts/makerdao/top-b353893.sol](../../contracts/makerdao/top-b353893.sol).

Repository https://github.com/makerdao/sai/blob/b353893f5ea8ee9811a446222c49b4812365cfe9/src/top.sol

<br />

<hr />

```javascript
/// top.sol -- global settlement manager

// Copyright (C) 2017  Nikolai Mushegian <nikolai@dapphub.com>
// Copyright (C) 2017  Daniel Brockman <daniel@dapphub.com>
// Copyright (C) 2017  Rain Break <rainbreak@riseup.net>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

// BK Ok
pragma solidity ^0.4.18;

// BK Next 2 Ok
import "./tub.sol";
import "./tap.sol";

// BK NOTE - `top`: Global Settlement Manager
// BK NOTE - top.owner=0x0000000000000000000000000000000000000000
// BK NOTE - top.authority=adm:0x8e2a84d6ade1e7fffee039a35ef5f19f13057152
// BK NOTE - Permit from top:0x9b0ccf7c8994e19f39b2b4cf708e0a7df65fa8a3 to tub:0x448a5065aebb8e423f0896e6c5d525c040f59af3 for cage(uint256,uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
// BK NOTE - Permit from top:0x9b0ccf7c8994e19f39b2b4cf708e0a7df65fa8a3 to tub:0x448a5065aebb8e423f0896e6c5d525c040f59af3 for flow() #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
// BK NOTE - Permit from top:0x9b0ccf7c8994e19f39b2b4cf708e0a7df65fa8a3 to tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef for cage(uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
// BK Ok
contract SaiTop is DSThing {
    // BK Next 3 Ok
    // BK NOTE - top.vox=vox:0x9b0f70df76165442ca6092939132bbaea77f2d7a
    // BK NOTE - top.tub=tub:0x448a5065aebb8e423f0896e6c5d525c040f59af3
    // BK NOTE - top.tap=tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef
    SaiVox   public  vox;
    SaiTub   public  tub;
    SaiTap   public  tap;

    // BK Next 4 Ok
    // BK NOTE - top.sai=sai:0x89d24a6b4ccb1b6faa2625fe562bdd9a23260359
    // BK NOTE - top.sin=sin:0x79f6d0f646706e1261acf0b93dcb864f357d4680
    // BK NOTE - top.skr=skr:0xf53ad2c6851052a81b42133467480961b2321c09
    // BK NOTE - top.gem=gem:0xc02aaa39b223fe8d0a0e5c4f27ead9083c756cc2
    DSToken  public  sai;
    DSToken  public  sin;
    DSToken  public  skr;
    ERC20    public  gem;

    // BK Next 4 Ok
    // BK NOTE - top.fix=0 0
    // BK NOTE - top.fit=0 0
    // BK NOTE - top.caged=0
    // BK NOTE - top.cooldown=21600
    uint256  public  fix;  // sai cage price (gem per sai)
    uint256  public  fit;  // skr cage price (ref per skr)
    uint256  public  caged;
    uint256  public  cooldown = 6 hours;


    // BK Ok - Constructor
    function SaiTop(SaiTub tub_, SaiTap tap_) public {
        // BK Next 2 Ok
        tub = tub_;
        tap = tap_;

        // BK Ok
        vox = tub.vox();

        // BK Next 4 Ok
        sai = tub.sai();
        sin = tub.sin();
        skr = tub.skr();
        gem = tub.gem();
    }

    // BK Ok - View function. Return current time
    function era() public view returns (uint) {
        // BK Ok
        return block.timestamp;
    }

    // force settlement of the system at a given price (sai per gem).
    // This is nearly the equivalent of biting all cups at once.
    // Important consideration: the gems associated with free skr can
    // be tapped to make sai whole.
    // BK NOTE - `cage`: shutting down the system and reimbursing Sai holders
    // BK Ok
    function cage(uint price) internal {
        // BK Ok - Can only execute once, when price is valid
        require(!tub.off() && price != 0);
        // BK Ok - Store time of this action
        caged = era();

        // BK Ok
        tub.drip();  // collect remaining fees
        // BK Ok - Cancel out Sin and Sai
        tap.heal();  // absorb any pending fees

        // BK Ok
        // BK NOTE - vox.par ~ RAY = 1000000000000000000000000000
        // BK NOTE - price ~ 602105000000000000000
        // BK NOTE - tub.per=1012902170729202921046965924
        // BK NOTE - wmul(price, vox.par()) = wmul(602105000000000000000, 1000000000000000000000000000) = 602105000000000000000000000000
        // BK NOTE - rmul(602105000000000000000000000000, 1012902170729202921046965924) = 609873461506906724776983417670
        fit = rmul(wmul(price, vox.par()), tub.per());
        // Most gems we can get per sai is the full balance of the tub.
        // If there is no sai issued, we should still be able to cage.
        if (sai.totalSupply() == 0) {
            // BK NOTE - WAD = 1000000000000000000
            // BK NOTE - price ~ 602105000000000000000
            // BK NOTE - fix = 1660839886730719724964915
            // BK NOTE - > new BigNumber("1660839886730719724964915").shift(-27)
            // BK NOTE - 0.001660839886730719724964915
            fix = rdiv(WAD, price);
        } else {
            // BK NOTE - tub.pie ~ 215638715251031372213368
            // BK NOTE - sai.totalSupply() ~ 42237506301505495848027942
            // BK NOTE - rdiv(tub.pie(), sai.totalSupply()) = 5105384624549798217100771
            // BK NOTE - > new BigNumber("5105384624549798217100771").shift(-27)
            // BK NOTE - 0.005105384624549798217100771
            // BK NOTE - So fix = min(1660839886730719724964915, 5105384624549798217100771)
            // BK NOTE - = 1660839886730719724964915
            fix = min(rdiv(WAD, price), rdiv(tub.pie(), sai.totalSupply()));
        }

        // BK Ok - `tub.cage(...)` will set `tub.off()` to true
        tub.cage(fit, rmul(fix, sai.totalSupply()));
        tap.cage(fix);

        tap.vent();    // burn pending sale skr
    }
    // cage by reading the last value from the feed for the price
    // BK Ok - Only adm can execute
    // BK NOTE - Global Settlement. `cage`: shutting down the system and reimbursing Sai holders
    function cage() public note auth {
        // BK Ok
        // BK NOTE - tub.pip().read will throw an error if a price is unavailable
        cage(rdiv(uint(tub.pip().read()), vox.par()));
    }

    // BK NOTE - `flow` enables exit after all CDPs are processed
    // BK Ok - Anyone can execute after cooldown period after global shutdown
    function flow() public note {
        // BK Ok - global shutdown already
        require(tub.off());
        // BK Ok - `din` = total debt backed by CDPs
        // BK NOTE - `fog`: SKR balance in `tap`, collateral pending liquidation
        var empty = tub.din() == 0 && tap.fog() == 0;
        // BK Ok
        var ended = era() > caged + cooldown;
        // BK Ok
        require(empty || ended);
        // BK Ok
        tub.flow();
    }

    // BK Ok - Only adm can set cooldown period after global settlement
    function setCooldown(uint cooldown_) public auth {
        // BK Ok
        cooldown = cooldown_;
    }
}

```
