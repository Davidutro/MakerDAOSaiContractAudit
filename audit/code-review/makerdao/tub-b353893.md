# tub-b353893

Source file [../../contracts/makerdao/tub-b353893.sol](../../contracts/makerdao/tub-b353893.sol).

Repository https://github.com/makerdao/sai/blob/b353893f5ea8ee9811a446222c49b4812365cfe9/src/tub.sol

<br />

<hr />

```javascript
/// tub.sol -- simplified CDP engine (baby brother of `vat')

// Copyright (C) 2017  Nikolai Mushegian <nikolai@dapphub.com>
// Copyright (C) 2017  Daniel Brockman <daniel@dapphub.com>
// Copyright (C) 2017  Rain Break <rainbreak@riseup.net>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

// BK Ok
pragma solidity ^0.4.18;

// BK Next 3 Ok
import "ds-thing/thing.sol";
import "ds-token/token.sol";
import "ds-value/value.sol";

// BK Ok
import "./vox.sol";

// BK Ok
contract SaiTubEvents {
    // BK Ok - Event
    event LogNewCup(address indexed lad, bytes32 cup);
}

// BK NOTE - `tub`: CDP Record Engine
// BK NOTE - tub.owner=0x0000000000000000000000000000000000000000
// BK NOTE - tub.authority=dad:0x315cbb88168396d12e1a255f9cb935408fe80710
// BK Ok
contract SaiTub is DSThing, SaiTubEvents {
    // BK Next 2 Ok
    // BK NOTE - tub.sai=sai:0x89d24a6b4ccb1b6faa2625fe562bdd9a23260359
    // BK NOTE - tub.sin=sin:0x79f6d0f646706e1261acf0b93dcb864f357d4680
    DSToken  public  sai;  // Stablecoin
    DSToken  public  sin;  // Debt (negative sai)

    // BK Next 2 Ok
    // BK NOTE - tub.skr=skr:0xf53ad2c6851052a81b42133467480961b2321c09
    // BK NOTE - tub.gem=gem:0xc02aaa39b223fe8d0a0e5c4f27ead9083c756cc2
    DSToken  public  skr;  // Abstracted collateral
    ERC20    public  gem;  // Underlying collateral

    // BK Ok
    // BK NOTE - tub.gov=gov:0x9f8f72aa9304c8b593d555f12ef6589cc3a579a2
    DSToken  public  gov;  // Governance token

    // BK Next 3 Ok
    // BK NOTE - tub.vox=vox:0x9b0f70df76165442ca6092939132bbaea77f2d7a
    // BK NOTE - tub.pip=pip:0x729d19f657bd0614b4985cf1d82531c67569197b
    // BK NOTE - tub.pep=pep:0x99041f808d598b782d5a3e498681c2452a31da08
    SaiVox   public  vox;  // Target price feed
    DSValue  public  pip;  // Reference price feed
    DSValue  public  pep;  // Governance price feed

    // BK Next 2 Ok
    // BK NOTE - tub.tap=tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef
    // BK NOTE - tub.pit=pit:0x69076e44a9c70a67d5b79d95795aba299083c275
    address  public  tap;  // Liquidator
    address  public  pit;  // Governance Vault

    // BK Next 6 Ok
    // BK NOTE - tub.axe=1130000000000000000000000000 1.13 ray
    // BK NOTE - tub.cap=50000000000000000000000000 50000000 sai
    // BK NOTE - tub.mat=1500000000000000000000000000 1.5 ray
    // BK NOTE - tub.tax=1000000000000000000000000000 1 ray
    // BK NOTE - tub.fee=1000000000158153903837946257 1.000000000158153903837946257 ray
    // BK NOTE - tub.gap=1000000000000000000
    uint256  public  axe;  // Liquidation penalty
    uint256  public  cap;  // Debt ceiling
    uint256  public  mat;  // Liquidation ratio
    uint256  public  tax;  // Stability fee
    uint256  public  fee;  // Governance fee
    uint256  public  gap;  // Join-Exit Spread

    // BK Next 2 Ok
    // BK NOTE - tub.off=false
    // BK NOTE - tub.out=false
    bool     public  off;  // Cage flag
    bool     public  out;  // Post cage exit

    // BK Ok
    // BK NOTE - tub.fit=0
    uint256  public  fit;  // REF per SKR (just before settlement)

    // BK Next 4 Ok
    // BK NOTE - tub.rho=1528335123 Thu, 07 Jun 2018 01:32:03 UTC Thu, 07 Jun 2018 11:32:03 AEST
    uint256  public  rho;  // Time of last drip
    uint256         _chi;  // Accumulated Tax Rates
    uint256         _rhi;  // Accumulated Tax + Fee Rates
    // BK NOTE - tub.rum=42158687814853524777974995 0.042158687814853524777974995 ray
    uint256  public  rum;  // Total normalised debt

    // BK Next 2 Ok
    // BK NOTE - tub.cupi=2196
    uint256                   public  cupi;
    mapping (bytes32 => Cup)  public  cups;

    // BK Next block Ok
    struct Cup {
        address  lad;      // CDP owner
        uint256  ink;      // Locked collateral (in SKR)
        uint256  art;      // Outstanding normalised debt (tax only)
        uint256  ire;      // Outstanding normalised debt
    }

    // BK Ok - View function - Get cup owner
    function lad(bytes32 cup) public view returns (address) {
        // BK Ok
        return cups[cup].lad;
    }
    // BK Ok - View function - Get locked collateral
    function ink(bytes32 cup) public view returns (uint) {
        // BK Ok
        return cups[cup].ink;
    }
    // BK Ok - Calculate normalised outstanding debt
    function tab(bytes32 cup) public returns (uint) {
        // BK Ok
        return rmul(cups[cup].art, chi());
    }
    // BK Ok - The difference between `rhi` and `chi` can be used to determine the total outstanding fee, the `rap`
    function rap(bytes32 cup) public returns (uint) {
        // BK Ok
        return sub(rmul(cups[cup].ire, rhi()), tab(cup));
    }

    // Total CDP Debt
    // BK Ok - Debt with the tax rate 
    function din() public returns (uint) {
        // BK Ok
        return rmul(rum, chi());
    }
    // Backing collateral
    // BK Ok - `air` = Skr balance
    function air() public view returns (uint) {
        // BK Ok
        return skr.balanceOf(this);
    }
    // Raw collateral
    // BK Ok - `pie` = read collateral (WETH) that Skr holders share
    // BK NOTE - ~ 215638715251031372213368
    // BK NOTE - > new BigNumber("215638715251031372213368").shift(-18)
    // BK NOTE - 215638.715251031372213368
    // BK NOTE - From EtherScan token balance - 215,638.715251031372213368 WETH
    function pie() public view returns (uint) {
        // BK Ok
        return gem.balanceOf(this);
    }

    //------------------------------------------------------------------

    // BK Ok - Constructor
    function SaiTub(
        DSToken  sai_,
        DSToken  sin_,
        DSToken  skr_,
        ERC20    gem_,
        DSToken  gov_,
        DSValue  pip_,
        DSValue  pep_,
        SaiVox   vox_,
        address  pit_
    ) public {
        // BK Next 2 Ok
        gem = gem_;
        skr = skr_;

        // BK Next 2 Ok
        sai = sai_;
        sin = sin_;

        // BK Next 2 Ok
        gov = gov_;
        pit = pit_;

        // BK Next 3 Ok
        pip = pip_;
        pep = pep_;
        vox = vox_;

        // BK Next 5 Ok
        axe = RAY;
        mat = RAY;
        tax = RAY;
        fee = RAY;
        gap = WAD;

        // BK Next 2 Ok
        _chi = RAY;
        _rhi = RAY;

        // BK Ok
        rho = era();
    }

    // BK Ok - Constant function - current time
    function era() public constant returns (uint) {
        // BK Ok
        return block.timestamp;
    }

    //--Risk-parameter-config-------------------------------------------

    // BK Ok - Only adm can call `mom.set*(...)` to change these parameters
    // BK NOTE - Permit from mom:0xf2c5369cffb8ea6284452b0326e326dbfdcb867c to tub:0x448a5065aebb8e423f0896e6c5d525c040f59af3 for mold(bytes32,uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
    function mold(bytes32 param, uint val) public note auth {
        // BK Next block Ok
        if      (param == 'cap') cap = val;
        else if (param == 'mat') { require(val >= RAY); mat = val; }
        else if (param == 'tax') { require(val >= RAY); drip(); tax = val; }
        else if (param == 'fee') { require(val >= RAY); drip(); fee = val; }
        else if (param == 'axe') { require(val >= RAY); axe = val; }
        else if (param == 'gap') { require(val >= WAD); gap = val; }
        else return;
    }

    //--Price-feed-setters----------------------------------------------

    // BK Ok - Only adm can call `mom.setPip(...)` to change these parameters
    // BK NOTE - Permit from mom:0xf2c5369cffb8ea6284452b0326e326dbfdcb867c to tub:0x448a5065aebb8e423f0896e6c5d525c040f59af3 for setPip(address) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
    function setPip(DSValue pip_) public note auth {
        // BK Ok
        pip = pip_;
    }
    // BK Ok - Only adm can call `mom.setPep(...)` to change these parameters
    // BK NOTE - Permit from mom:0xf2c5369cffb8ea6284452b0326e326dbfdcb867c to tub:0x448a5065aebb8e423f0896e6c5d525c040f59af3 for setPep(address) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
    function setPep(DSValue pep_) public note auth {
        // BK Ok
        pep = pep_;
    }
    // BK Ok - Only adm can call `mom.setVox(...)` to change these parameters
    // BK NOTE - Permit from mom:0xf2c5369cffb8ea6284452b0326e326dbfdcb867c to tub:0x448a5065aebb8e423f0896e6c5d525c040f59af3 for setVox(address) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
    function setVox(SaiVox vox_) public note auth {
        // BK Ok
        vox = vox_;
    }

    //--Tap-setter------------------------------------------------------
    // BK Ok - Any account can execute, but this can only be executed once
    // BK NOTE - tub.tap=tap:0xbda109309f9fafa6dd6a9cb9f1df4085b27ee8ef
    function turn(address tap_) public note {
        // BK Ok
        require(tap  == 0);
        // BK Ok
        require(tap_ != 0);
        // BK Ok
        tap = tap_;
    }

    //--Collateral-wrapper----------------------------------------------

    // Wrapper ratio (gem per skr)
    // BK NOTE - tub.per=1.012902170729202921046966079
    function per() public view returns (uint ray) {
        return skr.totalSupply() == 0 ? RAY : rdiv(pie(), skr.totalSupply());
    }
    // Join price (gem per skr)
    // BK NOTE - tub.ask(1e18)=1.012902170729202921
    function ask(uint wad) public view returns (uint) {
        return rmul(wad, wmul(per(), gap));
    }
    // Exit price (gem per skr)
    // BK NOTE - tub.bid(1e18)=1.012902170729202921
    function bid(uint wad) public view returns (uint) {
        return rmul(wad, wmul(per(), sub(2 * WAD, gap)));
    }
    function join(uint wad) public note {
        require(!off);
        require(ask(wad) > 0);
        require(gem.transferFrom(msg.sender, this, ask(wad)));
        // BK NOTE - Permit from tub:0x448a5065aebb8e423f0896e6c5d525c040f59af3 to skr:0xf53ad2c6851052a81b42133467480961b2321c09 for mint(address,uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
        skr.mint(msg.sender, wad);
    }
    function exit(uint wad) public note {
        require(!off || out);
        require(gem.transfer(msg.sender, bid(wad)));
        // BK NOTE - Permit from tub:0x448a5065aebb8e423f0896e6c5d525c040f59af3 to skr:0xf53ad2c6851052a81b42133467480961b2321c09 for burn(address,uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
        skr.burn(msg.sender, wad);
    }

    //--Stability-fee-accumulation--------------------------------------

    // Accumulated Rates
    // BK Ok - Any account can execute. Calculate and return latest accumulated tax rates
    function chi() public returns (uint) {
        // BK Ok
        drip();
        // BK Ok
        return _chi;
    }
    // BK Ok - Any account can execute. Calculate and return latest accumulated tax + fee rates
    function rhi() public returns (uint) {
        // BK Ok
        drip();
        // BK Ok
        return _rhi;
    }
    // BK Ok - Any account can execute
    function drip() public note {
        // BK Ok - While system running normally
        if (off) return;

        // BK Ok - Current time
        var rho_ = era();
        // BK Ok - Seconds since last calculation
        var age = rho_ - rho;
        // BK Ok - Executing in the same block - timestamp is the same as before
        if (age == 0) return;    // optimised
        // BK Ok - Save last calculation time
        rho = rho_;

        // BK Ok
        var inc = RAY;

        if (tax != RAY) {  // optimised
            var _chi_ = _chi;
            inc = rpow(tax, age);
            _chi = rmul(_chi, inc);
            // BK NOTE - Permit from tub:0x448a5065aebb8e423f0896e6c5d525c040f59af3 to sai:0x89d24a6b4ccb1b6faa2625fe562bdd9a23260359 for mint(address,uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
            sai.mint(tap, rmul(sub(_chi, _chi_), rum));
        }

        // optimised
        if (fee != RAY) inc = rmul(inc, rpow(fee, age));
        if (inc != RAY) _rhi = rmul(_rhi, inc);
    }


    //--CDP-risk-indicator----------------------------------------------

    // Abstracted collateral price (ref per skr)
    function tag() public view returns (uint wad) {
        return off ? fit : wmul(per(), uint(pip.read()));
    }
    // Returns true if cup is well-collateralized
    // BK Ok
    function safe(bytes32 cup) public returns (bool) {
        // BK Ok - tag = ref / Skr, ink = locked Skr
        var pro = rmul(tag(), ink(cup));
        // BK Ok - par = 1 USD
        var con = rmul(vox.par(), tab(cup));
        var min = rmul(con, mat);
        return pro >= min;
    }


    //--CDP-operations--------------------------------------------------

    // BK Ok - Create a new cup
    function open() public note returns (bytes32 cup) {
        // BK Ok - While system running normally
        require(!off);
        // BK Ok
        cupi = add(cupi, 1);
        // BK Ok
        cup = bytes32(cupi);
        // BK Ok - Owner
        cups[cup].lad = msg.sender;
        // BK Ok - Log event
        LogNewCup(msg.sender, cup);
    }
    // BK Ok - Transfer cup to another owner
    function give(bytes32 cup, address guy) public note {
        // BK Ok - Check executing account is the current owner
        require(msg.sender == cups[cup].lad);
        // BK Ok
        require(guy != 0);
        // BK Ok - Transfer ownership
        cups[cup].lad = guy;
    }

    // BK Ok - Any account can execute this, but must have sufficient SKR to transfer and have already approved for the transferFrom request
    function lock(bytes32 cup, uint wad) public note {
        // BK Ok - While system running normally
        require(!off);
        // BK Ok
        cups[cup].ink = add(cups[cup].ink, wad);
        // BK Ok - `transferFrom(...)`
        skr.pull(msg.sender, wad);
        // BK Ok - Check that the cup has either 0  or > 0.005 ETH minimum
        require(cups[cup].ink == 0 || cups[cup].ink > 0.005 ether);
    }
    // BK Ok - Cup owner account can execute
    function free(bytes32 cup, uint wad) public note {
        // BK Ok - Check ownership
        require(msg.sender == cups[cup].lad);
        // BK Ok
        cups[cup].ink = sub(cups[cup].ink, wad);
        // BK Ok - Effectively a `transfer(...)`
        skr.push(msg.sender, wad);
        // BK Ok - Check that the cup has sufficient collateralisation
        require(safe(cup));
        // BK Ok - Check the cup has either 0 or > 0.005 ETH minimum
        require(cups[cup].ink == 0 || cups[cup].ink > 0.005 ether);
    }

    // BK Ok - Cup owner account can execute to mint new Sai
    // BK NOTE - Permit from tub:0x448a5065aebb8e423f0896e6c5d525c040f59af3 to sai:0x89d24a6b4ccb1b6faa2625fe562bdd9a23260359 for mint(address,uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
    function draw(bytes32 cup, uint wad) public note {
        // BK Ok - While system running normally
        require(!off);
        // BK Ok - Check ownership
        require(msg.sender == cups[cup].lad);
        // BK Ok
        require(rdiv(wad, chi()) > 0);

        // BK Ok - Add to the debt
        cups[cup].art = add(cups[cup].art, rdiv(wad, chi()));
        // BK Ok - Add to the total normalised debt
        rum = add(rum, rdiv(wad, chi()));

        // BK Ok - Cup's normalised debt
        cups[cup].ire = add(cups[cup].ire, rdiv(wad, rhi()));
        // BK Ok - Mint new Sai
        sai.mint(cups[cup].lad, wad);

        // BK Ok - Check collateralisation
        require(safe(cup));
        // BK Ok - Check we are below the global cap still
        require(sai.totalSupply() <= cap);
    }
    // BK Ok
    // BK NOTE - Permit from tub:0x448a5065aebb8e423f0896e6c5d525c040f59af3 to sai:0x89d24a6b4ccb1b6faa2625fe562bdd9a23260359 for burn(address,uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
    function wipe(bytes32 cup, uint wad) public note {
        // BK Ok - While system running normally
        require(!off);

        // BK Ok
        var owe = rmul(wad, rdiv(rap(cup), tab(cup)));

        cups[cup].art = sub(cups[cup].art, rdiv(wad, chi()));
        rum = sub(rum, rdiv(wad, chi()));

        cups[cup].ire = sub(cups[cup].ire, rdiv(add(wad, owe), rhi()));
        sai.burn(msg.sender, wad);

        var (val, ok) = pep.peek();
        if (ok && val != 0) gov.move(msg.sender, pit, wdiv(owe, uint(val)));
    }

    function shut(bytes32 cup) public note {
        // BK Ok - While system running normally
        require(!off);
        require(msg.sender == cups[cup].lad);
        if (tab(cup) != 0) wipe(cup, tab(cup));
        if (ink(cup) != 0) free(cup, ink(cup));
        delete cups[cup];
    }

    // BK Ok - Any account can execute for unsafe CDPs to be liquidated
    function bite(bytes32 cup) public note {
        // BK Ok - `cup` not safe, or system already shut down
        require(!safe(cup) || off);

        // Take on all of the debt, except unpaid fees
        // BK Ok - CDP debt
        var rue = tab(cup);
        // BK Ok - Mint Sin for `tap`
        // BK NOTE - Permit from tub:0x448a5065aebb8e423f0896e6c5d525c040f59af3 to sin:0x79f6d0f646706e1261acf0b93dcb864f357d4680 for mint(address,uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
        sin.mint(tap, rue);
        // BK Ok - Total debt
        rum = sub(rum, cups[cup].art);
        cups[cup].art = 0;
        cups[cup].ire = 0;

        // Amount owed in SKR, including liquidation penalty
        var owe = rdiv(rmul(rmul(rue, axe), vox.par()), tag());

        if (owe > cups[cup].ink) {
            owe = cups[cup].ink;
        }

        skr.push(tap, owe);
        cups[cup].ink = sub(cups[cup].ink, owe);
    }

    //------------------------------------------------------------------

    // BK Ok - `top` can execute to shut down the system
    // BK NOTE - Permit from top:0x9b0ccf7c8994e19f39b2b4cf708e0a7df65fa8a3 to tub:0x448a5065aebb8e423f0896e6c5d525c040f59af3 for cage(uint256,uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
    function cage(uint fit_, uint jam) public note auth {
        // BK Ok - Not already been shutdown and `fit_` (ref per Skr after cage) is non-0
        require(!off && fit_ != 0);
        // BK Ok - `off` flag
        off = true;
        // BK Ok - Liquidation penalty
        axe = RAY;
        // BK Ok - buy/sell gap reset
        gap = WAD;
        // BK Ok - Set ref per Skr after cage
        fit = fit_;         // ref per skr
        // BK Ok
        require(gem.transfer(tap, jam));
    }
    // BK Ok - `top` can execute. `flow` enables exit after all CDPs are processed
    // BK NOTE - Permit from top:0x9b0ccf7c8994e19f39b2b4cf708e0a7df65fa8a3 to tub:0x448a5065aebb8e423f0896e6c5d525c040f59af3 for flow() #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
    function flow() public note auth {
        // BK Ok - System shut down
        require(off);
        // BK Ok - Allow 
        out = true;
    }
}

```
