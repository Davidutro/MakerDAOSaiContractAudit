# tub-b353893

Source file [../../contracts/makerdao/tub-b353893.sol](../../contracts/makerdao/tub-b353893.sol).

Repository https://github.com/makerdao/sai/blob/b353893f5ea8ee9811a446222c49b4812365cfe9/src/tub.sol

<br />

<hr />

```javascript
/// tub.sol -- simplified CDP engine (baby brother of `vat')

// Copyright (C) 2017  Nikolai Mushegian <nikolai@dapphub.com>
// Copyright (C) 2017  Daniel Brockman <daniel@dapphub.com>
// Copyright (C) 2017  Rain Break <rainbreak@riseup.net>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

pragma solidity ^0.4.18;

import "ds-thing/thing.sol";
import "ds-token/token.sol";
import "ds-value/value.sol";

import "./vox.sol";

contract SaiTubEvents {
    event LogNewCup(address indexed lad, bytes32 cup);
}

contract SaiTub is DSThing, SaiTubEvents {
    // BK Next 2 Ok
    DSToken  public  sai;  // Stablecoin
    DSToken  public  sin;  // Debt (negative sai)

    // BK Next 2 Ok
    DSToken  public  skr;  // Abstracted collateral
    ERC20    public  gem;  // Underlying collateral

    // BK Ok
    DSToken  public  gov;  // Governance token

    // BK Next 3 Ok
    SaiVox   public  vox;  // Target price feed
    DSValue  public  pip;  // Reference price feed
    DSValue  public  pep;  // Governance price feed

    // BK Next 2 Ok
    address  public  tap;  // Liquidator
    address  public  pit;  // Governance Vault

    // BK Next 6 Ok
    uint256  public  axe;  // Liquidation penalty
    uint256  public  cap;  // Debt ceiling
    uint256  public  mat;  // Liquidation ratio
    uint256  public  tax;  // Stability fee
    uint256  public  fee;  // Governance fee
    uint256  public  gap;  // Join-Exit Spread

    // BK Next 2 Ok
    bool     public  off;  // Cage flag
    bool     public  out;  // Post cage exit

    uint256  public  fit;  // REF per SKR (just before settlement)

    uint256  public  rho;  // Time of last drip
    uint256         _chi;  // Accumulated Tax Rates
    uint256         _rhi;  // Accumulated Tax + Fee Rates
    uint256  public  rum;  // Total normalised debt

    uint256                   public  cupi;
    mapping (bytes32 => Cup)  public  cups;

    struct Cup {
        address  lad;      // CDP owner
        uint256  ink;      // Locked collateral (in SKR)
        uint256  art;      // Outstanding normalised debt (tax only)
        uint256  ire;      // Outstanding normalised debt
    }

    // BK Ok - View function - Get cup owner
    function lad(bytes32 cup) public view returns (address) {
        // BK Ok
        return cups[cup].lad;
    }
    // BK Ok - View function - Get locked collateral
    function ink(bytes32 cup) public view returns (uint) {
        // BK Ok
        return cups[cup].ink;
    }
    // BK Ok - Calculate normalised outstanding debt
    function tab(bytes32 cup) public returns (uint) {
        // BK Ok
        return rmul(cups[cup].art, chi());
    }
    function rap(bytes32 cup) public returns (uint) {
        return sub(rmul(cups[cup].ire, rhi()), tab(cup));
    }

    // Total CDP Debt
    // BK 
    function din() public returns (uint) {
        return rmul(rum, chi());
    }
    // Backing collateral
    // BK Ok - `air` = Skr balance
    function air() public view returns (uint) {
        // BK Ok
        return skr.balanceOf(this);
    }
    // Raw collateral
    // BK Ok - `pie` = read collateral (WETH) that Skr holders share
    function pie() public view returns (uint) {
        // BK Ok
        return gem.balanceOf(this);
    }

    //------------------------------------------------------------------

    function SaiTub(
        DSToken  sai_,
        DSToken  sin_,
        DSToken  skr_,
        ERC20    gem_,
        DSToken  gov_,
        DSValue  pip_,
        DSValue  pep_,
        SaiVox   vox_,
        address  pit_
    ) public {
        gem = gem_;
        skr = skr_;

        sai = sai_;
        sin = sin_;

        gov = gov_;
        pit = pit_;

        pip = pip_;
        pep = pep_;
        vox = vox_;

        axe = RAY;
        mat = RAY;
        tax = RAY;
        fee = RAY;
        gap = WAD;

        _chi = RAY;
        _rhi = RAY;

        rho = era();
    }

    function era() public constant returns (uint) {
        return block.timestamp;
    }

    //--Risk-parameter-config-------------------------------------------

    function mold(bytes32 param, uint val) public note auth {
        if      (param == 'cap') cap = val;
        else if (param == 'mat') { require(val >= RAY); mat = val; }
        else if (param == 'tax') { require(val >= RAY); drip(); tax = val; }
        else if (param == 'fee') { require(val >= RAY); drip(); fee = val; }
        else if (param == 'axe') { require(val >= RAY); axe = val; }
        else if (param == 'gap') { require(val >= WAD); gap = val; }
        else return;
    }

    //--Price-feed-setters----------------------------------------------

    function setPip(DSValue pip_) public note auth {
        pip = pip_;
    }
    function setPep(DSValue pep_) public note auth {
        pep = pep_;
    }
    function setVox(SaiVox vox_) public note auth {
        vox = vox_;
    }

    //--Tap-setter------------------------------------------------------
    function turn(address tap_) public note {
        require(tap  == 0);
        require(tap_ != 0);
        tap = tap_;
    }

    //--Collateral-wrapper----------------------------------------------

    // Wrapper ratio (gem per skr)
    function per() public view returns (uint ray) {
        return skr.totalSupply() == 0 ? RAY : rdiv(pie(), skr.totalSupply());
    }
    // Join price (gem per skr)
    function ask(uint wad) public view returns (uint) {
        return rmul(wad, wmul(per(), gap));
    }
    // Exit price (gem per skr)
    function bid(uint wad) public view returns (uint) {
        return rmul(wad, wmul(per(), sub(2 * WAD, gap)));
    }
    function join(uint wad) public note {
        require(!off);
        require(ask(wad) > 0);
        require(gem.transferFrom(msg.sender, this, ask(wad)));
        skr.mint(msg.sender, wad);
    }
    function exit(uint wad) public note {
        require(!off || out);
        require(gem.transfer(msg.sender, bid(wad)));
        skr.burn(msg.sender, wad);
    }

    //--Stability-fee-accumulation--------------------------------------

    // Accumulated Rates
    // BK Ok - Accumulated rates
    function chi() public returns (uint) {
        drip();
        return _chi;
    }
    function rhi() public returns (uint) {
        drip();
        return _rhi;
    }
    function drip() public note {
        if (off) return;

        var rho_ = era();
        var age = rho_ - rho;
        if (age == 0) return;    // optimised
        rho = rho_;

        var inc = RAY;

        if (tax != RAY) {  // optimised
            var _chi_ = _chi;
            inc = rpow(tax, age);
            _chi = rmul(_chi, inc);
            sai.mint(tap, rmul(sub(_chi, _chi_), rum));
        }

        // optimised
        if (fee != RAY) inc = rmul(inc, rpow(fee, age));
        if (inc != RAY) _rhi = rmul(_rhi, inc);
    }


    //--CDP-risk-indicator----------------------------------------------

    // Abstracted collateral price (ref per skr)
    function tag() public view returns (uint wad) {
        return off ? fit : wmul(per(), uint(pip.read()));
    }
    // Returns true if cup is well-collateralized
    // BK Ok
    function safe(bytes32 cup) public returns (bool) {
        // BK Ok - tag = ref / Skr, ink = locked Skr
        var pro = rmul(tag(), ink(cup));
        // BK Ok - par = 1 USD
        var con = rmul(vox.par(), tab(cup));
        var min = rmul(con, mat);
        return pro >= min;
    }


    //--CDP-operations--------------------------------------------------

    // BK Ok - Create a new cup
    function open() public note returns (bytes32 cup) {
        // BK Ok - While system running normally
        require(!off);
        // BK Ok
        cupi = add(cupi, 1);
        // BK Ok
        cup = bytes32(cupi);
        // BK Ok - Owner
        cups[cup].lad = msg.sender;
        // BK Ok - Log event
        LogNewCup(msg.sender, cup);
    }
    // BK Ok - Transfer cup to another owner
    function give(bytes32 cup, address guy) public note {
        // BK Ok - Check executing account is the current owner
        require(msg.sender == cups[cup].lad);
        // BK Ok
        require(guy != 0);
        // BK Ok - Transfer ownership
        cups[cup].lad = guy;
    }

    // BK Ok - Any account can execute this, but must have sufficient SKR to transfer and have already approved for the transferFrom request
    function lock(bytes32 cup, uint wad) public note {
        // BK Ok - While system running normally
        require(!off);
        // BK Ok
        cups[cup].ink = add(cups[cup].ink, wad);
        // BK Ok - `transferFrom(...)`
        skr.pull(msg.sender, wad);
        // BK Ok - Check that the cup has either 0  or > 0.005 ETH minimum
        require(cups[cup].ink == 0 || cups[cup].ink > 0.005 ether);
    }
    // BK Ok - Cup owner account can execute
    function free(bytes32 cup, uint wad) public note {
        // BK Ok - Check ownership
        require(msg.sender == cups[cup].lad);
        // BK Ok
        cups[cup].ink = sub(cups[cup].ink, wad);
        // BK Ok - Effectively a `transfer(...)`
        skr.push(msg.sender, wad);
        // BK Ok - Check that the cup has sufficient collateralisation
        require(safe(cup));
        // BK Ok - Check the cup has either 0 or > 0.005 ETH minimum
        require(cups[cup].ink == 0 || cups[cup].ink > 0.005 ether);
    }

    // BK Ok - Cup owner account can execute to mint new Sai
    function draw(bytes32 cup, uint wad) public note {
        // BK Ok - While system running normally
        require(!off);
        // BK Ok - Check ownership
        require(msg.sender == cups[cup].lad);
        // BK Ok
        require(rdiv(wad, chi()) > 0);

        // BK Ok - Add to the debt
        cups[cup].art = add(cups[cup].art, rdiv(wad, chi()));
        // BK Ok - Add to the total normalised debt
        rum = add(rum, rdiv(wad, chi()));

        // BK Ok - Cup's normalised debt
        cups[cup].ire = add(cups[cup].ire, rdiv(wad, rhi()));
        // BK Ok - Mint new Sai
        sai.mint(cups[cup].lad, wad);

        // BK Ok - Check collateralisation
        require(safe(cup));
        // BK Ok - Check we are below the global cap still
        require(sai.totalSupply() <= cap);
    }
    // BK Ok
    function wipe(bytes32 cup, uint wad) public note {
        // BK Ok - While system running normally
        require(!off);

        // BK Ok
        var owe = rmul(wad, rdiv(rap(cup), tab(cup)));

        cups[cup].art = sub(cups[cup].art, rdiv(wad, chi()));
        rum = sub(rum, rdiv(wad, chi()));

        cups[cup].ire = sub(cups[cup].ire, rdiv(add(wad, owe), rhi()));
        sai.burn(msg.sender, wad);

        var (val, ok) = pep.peek();
        if (ok && val != 0) gov.move(msg.sender, pit, wdiv(owe, uint(val)));
    }

    function shut(bytes32 cup) public note {
        // BK Ok - While system running normally
        require(!off);
        require(msg.sender == cups[cup].lad);
        if (tab(cup) != 0) wipe(cup, tab(cup));
        if (ink(cup) != 0) free(cup, ink(cup));
        delete cups[cup];
    }

    // BK Ok - Any account can execute for unsafe CDPs to be liquidated
    function bite(bytes32 cup) public note {
        // BK Ok - `cup` not safe, or system already shut down
        require(!safe(cup) || off);

        // Take on all of the debt, except unpaid fees
        // BK Ok - CDP debt
        var rue = tab(cup);
        // BK Ok - Mint Sin
        sin.mint(tap, rue);
        // BK Ok - Total debt
        rum = sub(rum, cups[cup].art);
        cups[cup].art = 0;
        cups[cup].ire = 0;

        // Amount owed in SKR, including liquidation penalty
        var owe = rdiv(rmul(rmul(rue, axe), vox.par()), tag());

        if (owe > cups[cup].ink) {
            owe = cups[cup].ink;
        }

        skr.push(tap, owe);
        cups[cup].ink = sub(cups[cup].ink, owe);
    }

    //------------------------------------------------------------------

    // BK Ok - `top` can execute to shut down the system
    function cage(uint fit_, uint jam) public note auth {
        // BK Ok - Not already been shutdown and `fit_` (ref per Skr after cage) is non-0
        require(!off && fit_ != 0);
        // BK Ok - `off` flag
        off = true;
        // BK Ok - Liquidation penalty
        axe = RAY;
        // BK Ok - buy/sell gap reset
        gap = WAD;
        // BK Ok - Set ref per Skr after cage
        fit = fit_;         // ref per skr
        // BK Ok - 
        require(gem.transfer(tap, jam));
    }
    // BK Ok - `top` can execute. `flow` enables exit after all CDPs are processed
    function flow() public note auth {
        // BK Ok - System shut down
        require(off);
        // BK Ok - Allow 
        out = true;
    }
}

```
