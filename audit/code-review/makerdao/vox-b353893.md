# vox-b353893

Source file [../../contracts/makerdao/vox-b353893.sol](../../contracts/makerdao/vox-b353893.sol).

Repository https://github.com/makerdao/sai/blob/b353893f5ea8ee9811a446222c49b4812365cfe9/src/vox.sol

<br />

<hr />

```javascript
/// vox.sol -- target price feed

// Copyright (C) 2016, 2017  Nikolai Mushegian <nikolai@dapphub.com>
// Copyright (C) 2016, 2017  Daniel Brockman <daniel@dapphub.com>
// Copyright (C) 2017        Rain Break <rainbreak@riseup.net>

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

// BK Ok
pragma solidity ^0.4.18;

// BK Ok
import "ds-thing/thing.sol";

// BK NOTE - vox.owner=0x0000000000000000000000000000000000000000
// BK NOTE - vox.authority=dad:0x315cbb88168396d12e1a255f9cb935408fe80710
// BK NOTE - Dad permissions
// BK NOTE - Permit from mom:0xf2c5369cffb8ea6284452b0326e326dbfdcb867c to vox:0x9b0f70df76165442ca6092939132bbaea77f2d7a for mold(bytes32,uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
// BK NOTE - Permit from mom:0xf2c5369cffb8ea6284452b0326e326dbfdcb867c to vox:0x9b0f70df76165442ca6092939132bbaea77f2d7a for tune(uint256) #4752020 0x5e9650d385807b339446b874125907c110e061f8d458c6f9b753ced5eae5bb26
// BK NOTE - Only SaiMom can execute `mold(...)` and `tune(...)`
// BK Ok
contract SaiVox is DSThing {
    // BK Ok - `par` is the Sai target price. 1 = 1 USD
    uint256  _par;
    // BK Ok - `way` is the rate of change per second of `par`
    uint256  _way;

    // BK Ok - `fix` is fixed to 1e+27 or 1
    uint256  public  fix;
    // BK NOTE - According to https://github.com/makerdao/sai/blob/master/DEVELOPING.md#vox-target-price-feed , `how` is meant to be set to 0
    // BK NOTE - for this iteration
    // BK Ok
    uint256  public  how;
    // BK Ok - `tau` is the last time `prod()`ded
    uint256  public  tau;

    // BK Ok - Constructor. Called with `10 ** 27` as the parameters from DaiFab
    function SaiVox(uint par_) public {
        // BK Ok - Set initially to 1e+27
        _par = fix = par_;
        // BK Ok - `_way` = `RAY` = 1e+27
        _way = RAY;
        // BK Ok - This is the timestamp of the initial deployment
        tau  = era();
    }

    // BK Ok - Return current time
    function era() public view returns (uint) {
        // BK Ok
        return block.timestamp;
    }

    // BK Ok - `SaiMom.setWay(uint ray)` can execute this function, with min/max -/+ 10% per day
    // BK NOTE - require(ray < 1000001100000000000000000000);  // 10% / day
    // BK NOTE - require(ray >  999998800000000000000000000);
    function mold(bytes32 param, uint val) public note auth {
        if (param == 'way') _way = val;
    }

    // Dai Target Price (ref per dai)
    // BK Ok - Any account can execute this function
    function par() public returns (uint) {
        // BK Ok
        prod();
        // BK Ok
        return _par;
    }
    // BK Ok - Any account can execute this function
    function way() public returns (uint) {
        // BK Ok
        prod();
        // BK Ok
        return _way;
    }

    // BK Ok - NOTE that no contract or account has the authority to execute this function. `fix` is fixed to 1e+27 or 1
    function tell(uint256 ray) public note auth {
        // BK Ok
        fix = ray;
    }
    // BK NOTE - According to https://github.com/makerdao/sai/blob/master/DEVELOPING.md#vox-target-price-feed , `how` is meant to be set to 0
    // BK NOTE - for this iteration, so this function should not be executed
    // BK Ok - `SaiMom.setHow(uint ray)` can execute this function
    function tune(uint256 ray) public note auth {
        // BK Ok - Currently set to 0
        how = ray;
    }

    // BK Ok - Any account can execute this function to calculate `_par`
    function prod() public note {
        // BK Ok - `age` is the number of seconds since the last prod()
        var age = era() - tau;
        // BK Ok - Remove duplicate execution in the same block (same timestamp)
        if (age == 0) return;  // optimised
        // BK Ok - `tau` set to the current time
        tau = era();

        // BK NOTE - if (_way != 1e27) {
        // BK NOTE -   _par = _par * _way ^ age
        // BK NOTE - }
        // BK NOTE - At max 10% / day, `_way` = 1000001100000000000000000000
        // BK NOTE - 1 day = 60 x 60 x 24 = 86,400
        // BK NOTE - `_par` can increase by a max factor of 1.099702784876423481070321283 per day
        // BK NOTE - At min 10% / day, `_way` = 999998800000000000000000000
        // BK NOTE - `_par` can decrease by a min factor of 0.901513679582627489887306676 per day
        if (_way != RAY) _par = rmul(_par, rpow(_way, age));  // optimised

        // BK NOTE - According to https://github.com/makerdao/sai/blob/master/DEVELOPING.md#vox-target-price-feed , `how` is meant to be set to 0
        // BK NOTE - for this iteration, so the statements following should never be executed
        if (how == 0) return;  // optimised
        // BK Ok
        var wag = int128(how * age);
        // BK Ok
        _way = inj(prj(_way) + (fix < _par ? wag : -wag));
    }

    // BK NOTE - if (x >= 0) {
    // BK NOTE -   return x + 1e27
    // BK NOTE - } else {
    // BK NOTE -   1e27 / (1e27 - x)
    // BK NOTE - }
    function inj(int128 x) internal pure returns (uint256) {
        return x >= 0 ? uint256(x) + RAY
            : rdiv(RAY, RAY + uint256(-x));
    }
    // BK NOTE - if (x >= 1e27) {
    // BK NOTE -   return x - 1e27
    // BK NOTE - } else {
    // BK NOTE -   1e27 - 1e27 / x
    // BK NOTE - }
    function prj(uint256 x) internal pure returns (int128) {
        return x >= RAY ? int128(x - RAY)
            : int128(RAY) - int128(rdiv(RAY, x));
    }
}

```
